# Maintainer: walker84837 <walker84837@gmail.com>

pkgname=aconcat
pkgver=0.1.0
pkgrel=1
pkgdesc="A CLI tool for concatenating multiple audio files into a single output file."
arch=('x86_64')
url="https://github.com/walker84837/aconcat"
license=('BSD-3-Clause')
depends=()
makedepends=('go' 'git')
provides=("$pkgname")
conflicts=()
source=("$pkgname::git+$url.git")
sha256sums=('SKIP')  # Skipping checksum verification for VCS sources

build() {
  cd "$srcdir/$pkgname"
  mkdir -p bin

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"

  go build -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    -o bin/ac src/*.go
}

package() {
  cd "$srcdir/$pkgname"
  install -Dm755 "bin/ac" "$pkgdir/usr/bin/ac"
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
